# 硬盘读写

## 硬盘读写的两种模式

- CHS 模式
- LBA 模式 逻辑块地址

### LBA模式的端口

端口是外部设备的内部的寄存器

| Primary 通道 | in操作 | out操作 |
| ------- | ------- | ------- |
| 0x1F0 | data | data |
| 0x1F1 | Error | features |
| 0x1F2 | Sector count | Sector count |
| 0x1F3 | LBW low | LBW low |
| 0x1F4 | LBA mid | LBA mid |
| 0x1F5 | LBA high | LBA high |
| 0x1F6 | Device | Device |
| 0x1F7 | Status | Command |

- 0x1F0:16bit 端口，用于读写数据，需要的数据储存在这里
- 0x1F1：检测前一个指令的错误
- 0x1F0  
    16bit端口，用于读写数据

- 0x1F1
    检测前一个指令的错误（无用）

- 0x1F2
    读写扇区的数量

- 0x1F3
    起始扇区的前8位

- 0x1F4
    中8位

- 0x1F5
    后八位

- 0x1F6
    0到3位 起始扇区的24到27位
    第四位 判断主盘和从片，0为主盘，1为从片
    5和7位默认为1
    第6为为1，指的是设置为LBA模式，0的话为CHS模式

- 0x1F7
    out
        0xEC   识别硬盘
        0x20  读取硬盘
        0x30  写硬盘

    in  8bit的数据
        0位   ERR错误标记
        3位  数据准备完毕标记 准备完毕为1
        7位 BSY  硬盘繁忙 繁忙为0，不忙为1

## 代码

xchg bx,bx;bochs 的魔术断点
mov edi, 0x1000;读到硬盘的哪个位置
mov ecx, 0;将要输入的扇区的数据放入ecx寄存器中
mov bl,1;读入几个扇区
call read_disk

xchg bx,bx;bochs 的魔术断点
mov edi, 0x1000;写到硬盘的哪个位置
mov ecx, 2;起始扇区
mov bl,1;读入几个扇区
call write_disk

xchg bx,bx;bochs 的魔术断点
;阻塞
   jmp $
read_disk:
   ;初始化寄存器的值
      ;设置第二个端口读取扇区的数量
      mov dx,0x1f2;将扇区数量的值传入端口寄存器
      mov al,bl;将读取扇区数量的值bl赋给al
      out dx,al;向0x1f2端口写入al的值

      inc dx;自增
      mov al,cl;将前八位放入al中
      out dx,al;向0x1f3输出al的值

      inc dx;自增
      shr ecx,8;将ecx右移8位
      mov al,cl;将中八位放入al中
      out dx,al;向0x1f4输出al的值

      inc dx;自增
      shr ecx,8;将ecx右移8位
      mov al,cl;将后八位放入al中
      out dx,al;向0x1f5输出al的值

      inc dx;自增0x1f6
      shr ecx,8;将ecx右移8位
      and cl,0b1111;进行与运算，相当于保留后四位，将前四位赋0
      mov al, 0b1110_0000;配置0x1f6的值
      or al,cl;将cl与al合并
      out dx,al;

      inc dx;自增
      mov al,0x20;读硬盘
      out dx,al;输出
      
      xor ecx,ecx;清空ecx
      mov cl,bl;得到读写扇区数量
      .read:
        push cx;保存cx的值进入栈中得到副本数据
         call .waits;等待数据准备完毕进行检查
         call .reads;读取操作
        pop cx;将副本数据恢复
         loop .read

         ret
      .waits: 
      mov dx,0x1f7
      .check:
         in al,dx
         jmp $+2;延迟
         jmp $+2
         jmp $+2
         and al,0b1000_1000;保留第三位和第七位，其他全赋0
         cmp al,0b0000_1000;比较这两个字符，会把结果存入ZF标志位
         jnz .check;起到了一个循环判断的作用
         ret

      .reads:
         mov dx,0x1f0;把读的数据放入dx中
          mov cx,256;设置一个扇区256个字改变了cx的值
      .readw:
         in ax,dx
         jmp $+2;延迟
         jmp $+2
         jmp $+2
         mov [edi] , ax
         add edi,2;往下递增地址 
         loop .readw
         ret
write_disk:
   ;初始化寄存器的值
      ;设置第二个端口读取扇区的数量
      mov dx,0x1f2;将扇区数量的值传入端口寄存器
      mov al,bl;将读取扇区数量的值bl赋给al
      out dx,al;向0x1f2端口写入al的值

      inc dx;自增
      mov al,cl;将前八位放入al中
      out dx,al;向0x1f3输出al的值

      inc dx;自增
      shr ecx,8;将ecx右移8位
      mov al,cl;将中八位放入al中
      out dx,al;向0x1f4输出al的值

      inc dx;自增
      shr ecx,8;将ecx右移8位
      mov al,cl;将后八位放入al中
      out dx,al;向0x1f5输出al的值

      inc dx;自增0x1f6
      shr ecx,8;将ecx右移8位
      and cl,0b1111;进行与运算，相当于保留后四位，将前四位赋0
      mov al, 0b1110_0000;配置0x1f6的值
      or al,cl;将cl与al合并
      out dx,al;

      inc dx;自增
      mov al,0x30;读硬盘
      out dx,al;输出
      
      xor ecx,ecx;清空ecx
      mov cl,bl;得到读写扇区数量
      .write:
        push cx;保存cx的值进入栈中得到副本数据
       call .writes;读取操作
      call .waits;等待数据准备完毕进行检查
        pop cx;将副本数据恢复
         loop .write

         ret
      .waits: 
      mov dx,0x1f7
      .check:
         in al,dx
         jmp $+2;延迟
         jmp $+2
         jmp $+2
         and al,0b1000_0000;保留第三位和第七位，其他全赋0
         cmp al,0b0000_0000;比较这两个字符，会把结果存入ZF标志位
         jnz .check;起到了一个循环判断的作用
         ret

      .writes:
         mov dx,0x1f0;把读的数据放入dx中
          mov cx,256;设置一个扇区256个字改变了cx的值
      .writew:
      mov  ax, [edi]
        out dx,ax
         jmp $+2;延迟
         jmp $+2
         jmp $+2
         
         add edi,2;往下递增地址 
         loop .writew
         ret
